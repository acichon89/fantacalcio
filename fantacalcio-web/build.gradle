buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.4'
    }
}

description = 'Main user interface - web application'

apply plugin: 'war'
apply plugin: 'com.bmuschko.tomcat'

dependencies {
	compile project (':fantacalcio-gamerules')
	compile 'org.springframework:spring-webmvc:4.1.7.RELEASE'
	providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
	
	dependencies {
	    def tomcatVersion = '7.0.59'
	    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
	           "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
	           "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
	}
}

ext {
    tomcatStopPort = 8081
    tomcatStopKey = 'stopKey'
}


task integrationTomcatRun(type: com.bmuschko.gradle.tomcat.tasks.TomcatRun) {
    stopPort = tomcatStopPort
    stopKey = tomcatStopKey
    daemon = true
}

task integrationTomcatStop(type: com.bmuschko.gradle.tomcat.tasks.TomcatStop) {
    stopPort = tomcatStopPort
    stopKey = tomcatStopKey
}

task acceptanceTest(type: Test) {
	systemProperty "spring.profiles.active", System.getProperty("spring.profiles.active")
	dependsOn integrationTomcatRun
    finalizedBy integrationTomcatStop
	testClassesDir = sourceSets.acceptanceTest.output.classesDir
	classpath = sourceSets.acceptanceTest.runtimeClasspath
	outputs.upToDateWhen { false }
	reports.junitXml.destination = file("$buildDir/acceptance-test-results/")
	reports.html.destination = file("$buildDir/reports/acceptance-tests/")
}

	check.dependsOn acceptanceTest
	acceptanceTest.dependsOn integrationTest
	integrationTest.dependsOn test

tomcat {
    httpPort = 8080
    httpsPort = 8091
    enableSSL = true
    contextPath = 'fantacalcio'

    users {
        user {
            username = 'user1'
            password = '123456'
            roles = ['developers', 'admin']
        }

        user {
            username = 'user2'
            password = 'abcdef'
            roles = ['manager']
        }
    }
}