buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.4'
    }
}

description = 'Main user interface - web application'

apply plugin: 'war'
apply plugin: 'com.bmuschko.tomcat'
apply plugin: 'scala'

sourceSets {
	acceptanceTest {
    	java {
    		srcDir file('src/test-acceptance/java');
    	}
    	resources.srcDir file('src/test-acceptance/resources')
        compileClasspath = compileClasspath + configurations.provided
    }
    loadTest {
    	scala {
    		srcDir file('src/test-load/scala');
    	}
    	resources.srcDir file('src/test-load/resources')
        compileClasspath = compileClasspath + configurations.provided
    }
}

dependencies {
	compile project (':fantacalcio-football')
	compile 'org.springframework:spring-webmvc:4.1.7.RELEASE'
	providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
	
	def tomcatVersion = '7.0.59'
	tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
	       "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
	       "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
	       
	acceptanceTestCompile sourceSets.main.output		
	acceptanceTestCompile configurations.testCompile
	acceptanceTestCompile configurations.testRuntime
	acceptanceTestCompile configurations.integrationTestCompile
	acceptanceTestCompile configurations.integrationTestRuntime
	
	loadTestCompile sourceSets.main.output		
	loadTestCompile configurations.testCompile
	loadTestCompile configurations.testRuntime
	loadTestCompile configurations.integrationTestCompile
	loadTestCompile configurations.integrationTestRuntime
	loadTestCompile configurations.acceptanceTestRuntime
	
	acceptanceTestCompile 	'org.seleniumhq.selenium:selenium-java:2.52.0',
    'org.seleniumhq.selenium:selenium-api:2.52.0',
    'org.seleniumhq.selenium:selenium-server:2.52.0',
    'org.seleniumhq.selenium:selenium-support:2.52.0',
    'org.seleniumhq.selenium:selenium-firefox-driver:2.52.0',
    'org.seleniumhq.selenium:selenium-ie-driver:2.52.0'   
    
    loadTestCompile 'io.gatling.highcharts:gatling-charts-highcharts:2.1.7'
    loadTestCompile 'io.gatling:gatling-core:2.1.7'
    loadTestCompile 'io.gatling:gatling-app:2.1.7'
    compile 'org.scala-lang:scala-compiler:2.11.2' 
    compile "org.scala-lang:scala-library:2.11.2"
}

ext {
    tomcatStopPort = 8081
    tomcatStopKey = 'stopKey'
}


task integrationTomcatRun(type: com.bmuschko.gradle.tomcat.tasks.TomcatRun) {
    stopPort = tomcatStopPort
    stopKey = tomcatStopKey
    daemon = true
}

task integrationTomcatStop(type: com.bmuschko.gradle.tomcat.tasks.TomcatStop) {
    stopPort = tomcatStopPort
    stopKey = tomcatStopKey
}

task acceptanceTest(type: Test) {
	systemProperty "spring.profiles.active", System.getProperty("spring.profiles.active")
	dependsOn integrationTomcatRun
    finalizedBy integrationTomcatStop
	testClassesDir = sourceSets.acceptanceTest.output.classesDir
	classpath = sourceSets.acceptanceTest.runtimeClasspath
	outputs.upToDateWhen { false }
	reports.junitXml.destination = file("$buildDir/acceptance-test-results/")
	reports.html.destination = file("$buildDir/reports/acceptance-tests/")
}

task loadTest(type: JavaExec) {
	systemProperty "spring.profiles.active", System.getProperty("spring.profiles.active")
	dependsOn integrationTomcatRun
    finalizedBy integrationTomcatStop
	 classpath = sourceSets.loadTest.runtimeClasspath
	 jvmArgs = [ '-Dgatling.core.directory.binaries=./build/classes/loadTest' ]
    // Gatling application
    main = "io.gatling.app.Gatling"
    // Specify the simulation to run
    args = Eval.me("['-s', 'com.javangarda.fantacalcio.simulations.BasicSimulation']")
}

task jacocoAcceptanceTestReport(type:JacocoReport){
	  group = "Reporting"
      description = "Generate Jacoco coverage reports after running acceptance tests."

      executionData = fileTree(dir: 'build/jacoco', include: 'acceptanceTest.exec')

      reports {
             xml{
                 enabled false
             }
             csv.enabled false
             html{
                 enabled true
                 //Following value is a folder
                 destination "${buildDir}/reports/jacoco/acceptance-test/html"
             }
      }


      sourceDirectories = files(sourceSets.main.allSource.srcDirs)
      classDirectories = files(sourceSets.main.output.classesDir)
}

/// IDE :
	eclipse {
	  classpath {
	    plusConfigurations += [ configurations.acceptanceTestCompile]
	    plusConfigurations += [ configurations.loadTestCompile]
	  }
	}

	check.dependsOn loadTest
	loadTest.dependsOn acceptanceTest
	acceptanceTest.dependsOn integrationTest
	integrationTest.dependsOn test

tomcat {
    httpPort = 8080
    httpsPort = 8091
    enableSSL = true
    contextPath = 'fantacalcio'
}